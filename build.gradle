import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

mainClassName = "com.slimebot.main.Main"

group 'com.slimebot'
version '2.6.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation 'net.dv8tion:JDA:5.0.0-beta.11'
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:8.0.0'
    implementation 'ch.qos.logback:logback-classic:1.4.8'
    implementation 'io.github.cdimascio:java-dotenv:5.1.1'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'org.jdbi:jdbi3-core:3.39.0'
    implementation 'org.jdbi:jdbi3-sqlobject:3.39.0'
    implementation 'org.jdbi:jdbi3-postgres:3.39.0'

    implementation 'com.github.MineKingBot:DiscordUtils:625de61'
}

String getVersion() {
    var version = project.version.toString()

    try {
        var commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        version += "-" + commit
    } catch(IOException ignore) {
        version += "-unknown"
    }

    return version
}

tasks.register("prepareSources", Copy) {
    delete("$buildDir/preparedSources")

    from("src/main/java") {
        include("**/BuildInfo.java")

        filter(ReplaceTokens, "tokens":
                Map.of(
                        "VERSION", getVersion()
                )
        )
    }

    into("$buildDir/preparedSources")

    includeEmptyDirs = false
}

compileJava {
    var javaSources = sourceSets["main"].allJava.filter {
        it.name != "BuildInfo.java"
    }.asFileTree

    source = javaSources + fileTree("$buildDir/preparedSources")

    dependsOn "prepareSources"

    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.slimebot.main.Main'
    }
}


test {
    useJUnitPlatform()
    systemProperty "file.encoding", "utf-8"
}