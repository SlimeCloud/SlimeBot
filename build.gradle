import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'

    id 'application'
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

mainClassName = "de.slimecloud.slimeball.main.Main"

group 'com.slimebot'
version '3.8.1'

repositories {
    mavenCentral()
    maven { url 'https://maven.mineking.dev/releases' }
    maven { url 'https://maven.mineking.dev/snapshots' }

    maven { url 'https://jitpack.io/' }
}

String lombokDependency = 'org.projectlombok:lombok:1.18.32'

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-beta.23'
    implementation 'de.mineking:DiscordUtils:3.9.0'

    implementation 'de.mineking:JavaUtils:1.9.0'
    implementation 'de.mineking:DatabaseUtils:442b063'
    implementation 'de.cyklon:ReflectionUtils:192873e'
    implementation 'de.cyklon:JEvent:879a575'

    implementation 'se.michaelthelin.spotify:spotify-web-api-java:9.0.0-RC1'
    implementation 'com.github.minndevelopment:emoji-java:master-SNAPSHOT'
    implementation 'org.kohsuke:github-api:1.321'

    implementation 'io.github.cdimascio:java-dotenv:5.2.2'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'org.sejda.imageio:webp-imageio:0.1.6'
    implementation 'ch.qos.logback:logback-classic:1.5.6'

    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.jdbi:jdbi3-postgres:3.45.1'

    compileOnly 'org.jetbrains:annotations:24.1.0'

    compileOnly lombokDependency
    annotationProcessor lombokDependency

    testCompileOnly lombokDependency
    testAnnotationProcessor lombokDependency

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0-M1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

String getVersion() {
    String version = project.version.toString()

    try {
        String commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        version += "-" + commit
    } catch (IOException ignore) {
        version += "-unknown"
    }

    return version
}

tasks.register("prepareSources", Copy) {
    delete("$buildDir/preparedSources")

    from("src/main/java") {
        include("**/BuildInfo.java")

        filter(ReplaceTokens, "tokens":
                Map.of(
                        "VERSION", getVersion()
                )
        )
    }

    into("$buildDir/preparedSources")

    includeEmptyDirs = false
}

compileJava {
    FileTree javaSources = sourceSets.main.allJava.filter {
        it.name != "BuildInfo.java"
    }.asFileTree

    source = javaSources + fileTree("$buildDir/preparedSources")

    dependsOn "prepareSources"

    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    archiveName = "SlimeBot.jar"
}

test {
    useJUnitPlatform()
    systemProperty "file.encoding", "utf-8"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
